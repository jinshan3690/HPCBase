/*全局配置信息及第三方依赖统一管理*/
project.ext {
    //统一设置APP配置
    setDefaultConfig = { extension ->
        //相对于APP中的build.gradle android {}的内容
        extension.android {
            compileSdkVersion rootProject.ext.android["compileSdkVersion"]
            buildToolsVersion rootProject.ext.android["buildToolsVersion"]
            defaultConfig {
                minSdkVersion rootProject.ext.android["minSdkVersion"]
                targetSdkVersion rootProject.ext.android["targetSdkVersion"]
                versionCode rootProject.ext.android["versionCode"]
                versionName rootProject.ext.android["versionName"]

                testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunne"
                multiDexEnabled true
            }

            buildTypes {
                release {//远程仓库不能混淆
                    minifyEnabled false
                    if (extension.android.defaultConfig.applicationId != null) {
                        //启用资源压缩
                        shrinkResources true
                    }
                    proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
                }
            }

            sourceSets {
                main {
                    jniLibs.srcDirs = ['lib']
                    if (rootProject.ext.isModule && file('src/main/module/AndroidManifest.xml').exists()) {
                        manifest.srcFile 'src/main/module/AndroidManifest.xml'
                    } else {
                        manifest.srcFile 'src/main/AndroidManifest.xml'
                        java {
                            //排除java/debug文件夹下的所有文件
                            exclude '*module'
                        }
                    }
                }
            }
            buildFeatures {
                dataBinding = true
            }
            compileOptions {
                sourceCompatibility JavaVersion.VERSION_1_8
                targetCompatibility JavaVersion.VERSION_1_8
            }
            kotlinOptions {
                jvmTarget = JavaVersion.VERSION_1_8.toString()
            }
        }

    }

}