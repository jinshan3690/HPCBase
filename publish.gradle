apply plugin: 'maven-publish'

ext{
    RELEASE_URL = 'https://ops-nexus.hpcang.com/repository/android-releases/'
    SNAPSHOT_URL = 'https://ops-nexus.hpcang.com/repository/android-snapshots/'

    //    MAVEN_VERSION = '1.0.0'
    MAVEN_VERSION = '1.0.0-SNAPSHOT'
}

//上传groupId名称
def MAVEN_GROUP_ID = 'com.hpcang.base'

//加载资源
Properties properties = new Properties()
InputStream inputStream = project.rootProject.file('local.properties').newDataInputStream()
properties.load( inputStream )

//读取字段
def MAVEN_USERNAME = properties.getProperty( 'username' )
def MAVEN_PASSWORD = properties.getProperty( 'password' )

// 判断版本是Release or Snapshots
def isReleaseBuild() {
    return !MAVEN_VERSION.contains("SNAPSHOT")
}

// 获取仓库url
def getRepositoryUrl() {
    return isReleaseBuild() ? RELEASE_URL : SNAPSHOT_URL
}

publishing {
    publications {
        uploadSDK(MavenPublication) {
            groupId MAVEN_GROUP_ID
            version MAVEN_VERSION
            pom.withXml {
                def dependenciesNode = asNode().appendNode('dependencies')
                configurations.implementation.allDependencies.withType(ModuleDependency) { ModuleDependency dp ->
                    if (dp.version != "unspecified") {
                        def dependencyNode = dependenciesNode.appendNode('dependency')
                        dependencyNode.appendNode('groupId', dp.group)
                        dependencyNode.appendNode('artifactId', dp.name)
                        dependencyNode.appendNode('version', dp.version)

                        if (dp.excludeRules.size() > 0) {
                            def exclusions = dependencyNode.appendNode('exclusions')
                            dp.excludeRules.each { ExcludeRule ex ->
                                def exclusion = exclusions.appendNode('exclusion')
                                exclusion.appendNode('groupId', ex.group)
                                exclusion.appendNode('artifactId', ex.module)
                            }
                        }
                    }
                }
            }
        }
    }

    repositories {
        maven {
            url getRepositoryUrl()
            credentials {
                username MAVEN_USERNAME
                password MAVEN_PASSWORD
            }
        }
    }

}


task publishToMaven(dependsOn: ['assemble', 'publish']) {
    doLast {
        println "finish publish to maven"
    }
}

publishToMaven.setGroup('upload')